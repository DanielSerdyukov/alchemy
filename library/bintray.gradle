apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = rootProject.ext.groupId
version = rootProject.ext.versionName

task sourcesJar(type: Jar) {
    from files('src/main/java')
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        mavenAndroid(MavenPublication) {
            pom { packaging 'aar' }
            artifact file('build/outputs/aar/library-release.aar')
            artifact sourcesJar
            pom.withXml {
                Node root = asNode()
                root.appendNode('name', 'rxsqlite')
                root.appendNode('description', 'Reactive extension of SQLite for Android')
                root.appendNode('url', 'https://github.com/DanielSerdyukov/rxsqlite')

                def issues = root.appendNode('issueManagement')
                issues.appendNode('system', 'github')
                issues.appendNode('url', 'https://github.com/DanielSerdyukov/rxsqlite/issues')

                def scm = root.appendNode('scm')
                scm.appendNode('url', 'scm:https://github.com/DanielSerdyukov/rxsqlite.git')
                scm.appendNode('connection', 'scm:https://github.com/DanielSerdyukov/rxsqlite.git')
                scm.appendNode('developerConnection', 'scm:https://github.com/DanielSerdyukov/rxsqlite.git')

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                def dependencies = root.appendNode('dependencies')
                ['compile'].each { scope ->
                    project.configurations[scope].allDependencies.each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.group)
                        dependency.appendNode('artifactId', it.name)
                        dependency.appendNode('version', it.version)
                        dependency.appendNode('scope', scope)
                    }
                }
            }
        }
    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    configurations = ['archives']
    publications = ['mavenAndroid']
    publish = true
    pkg {
        repo = 'maven'
        name = 'rxsqlite'
        licenses = ['Apache-2.0']
        version {
            name = rootProject.ext.versionName
            vcsTag = rootProject.ext.versionName
        }
    }
}
